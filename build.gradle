buildscript {
    ext {
        kotlin_version = '1.3.61'
        test_version = "4.0.0"
        asoft_phone_version = "1.0.0"
        asoft_persist_version = "15.0.0"
        android_build_tools_version = "3.5.2"
        serialization_version = "0.14.0"
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_build_tools_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath("org.jetbrains.kotlin:kotlin-serialization:$kotlin_version")
    }
}

plugins {
    id 'kotlin-multiplatform' version '1.3.61'
}

group 'tz.co.asoft'
version '1.0.0'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 1
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
            resources.srcDirs = ['src/androidMain/resources']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    google()
    jcenter()
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-js-wrappers' }
    maven { url 'https://dl.bintray.com/kotlinx/kotlinx' }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

kotlin {
    jvm() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    android() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
        publishLibraryVariants("release")
    }

    js() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                kotlinOptions.metaInfo = true
                kotlinOptions.sourceMap = true
                kotlinOptions.moduleKind = 'plain'
                kotlinOptions.main = "call"
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                api "com.github.andylamax.asoft-phone:asoft-phone-metadata:$asoft_phone_version"
                api "com.github.andylamax.asoft-persist:asoft-persist-metadata:$asoft_persist_version"
            }
        }

        commonTest {
            dependencies {
                implementation "com.github.andylamax.asoft-test:asoft-test-metadata:$test_version"
            }
        }

        androidMain {
            dependencies {
                implementation kotlin('stdlib')
                api "com.github.andylamax.asoft-phone:asoft-phone-android:$asoft_phone_version"
                api "com.github.andylamax.asoft-persist:asoft-persist-android:$asoft_persist_version"
            }
        }

        androidTest {
            dependencies {
                implementation "com.github.andylamax.asoft-test:asoft-test-android:$test_version"
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib')
                api "com.github.andylamax.asoft-phone:asoft-phone-jvm:$asoft_phone_version"
                api "com.github.andylamax.asoft-persist:asoft-persist-jvm:$asoft_persist_version"
            }
        }

        jvmTest {
            dependencies {
                implementation "com.github.andylamax.asoft-test:asoft-test-jvm:$test_version"
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                api "com.github.andylamax.asoft-phone:asoft-phone-js:$asoft_phone_version"
                api "com.github.andylamax.asoft-persist:asoft-persist-js:$asoft_persist_version"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
                implementation "com.github.andylamax.asoft-test:asoft-test-js:$test_version"
            }
        }
    }
}